class Customer(var name: string) {
  private var rentals = array();

  public fun addRental(rental: Rental) {
    array_push(this.rentals, rental);
  }

  public fun getName(): string {
    return this.name;
  }

  public fun statement(): string {
    var totalAmount = 0;
    var frequentRenterPoints = 0;
    var result = "Rental Record for " +. this.getName() +. "\n";

    // determine amounts for each line
    foreach (this.rentals as rental) {
      var thisAmount = 0;

      if (rental.getMovie().getPriceCode() == Movie::REGULAR) {
        thisAmount += 2;
        if (rental.getDaysRented() > 2) {
          thisAmount += (rental.getDaysRented() - 2) * 1.5;
        }
      } else if (rental.getMovie().getPriceCode() == Movie::NEW_RELEASE) {
        thisAmount += rental.getDaysRented() * 3;
      } else if (rental.getMovie().getPriceCode() == Movie::CHILDRENS) {
        thisAmount += 1.5;
        if (rental.getDaysRented() > 3) {
          thisAmount += (rental.getDaysRented() - 3) * 1.5;
        }
      }

      frequentRenterPoints++;
      if (rental.getMovie().getPriceCode() == Movie::NEW_RELEASE) {
        if (rental.getDaysRented() > 1) {
          frequentRenterPoints++;
        }
      }

      result +.= "\t" +. rental.getMovie().getTitle() +. "\t" +. thisAmount +. "\n";
      totalAmount += thisAmount;
    }

    result +.= "Amount owed is " +. totalAmount +. "\n";
    result +.= "You earned " +. frequentRenterPoints +. " frequent renter points";
    return result;
  }
}
